<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="FileFinder" Id="{7ae37c03-cd82-482d-8069-507c0dbb36f3}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'enable_dynamic_creation'}
{attribute 'no_explicit_call' := 'do not call this FB directly'}
FUNCTION_BLOCK PUBLIC FileFinder IMPLEMENTS I_FileFinder
VAR
	referencedFileCollection : I_FileSystemEntryCollection;
	status : (INIT, FIRST_READ, NEXT_READ, WAITING_FOR_NOT_BUSY, CHECK_EOE, PROCESS_FOUND_ENTRY, FOUND_DIRECTORY, FOUND_FILE, FINISHED) := INIT;
	searchDir: FilePath;
	searchString : T_MaxString;
	fileExt : FileExtension;
	fbFindFileEntry : FB_EnumFindFileEntry;
	fileEntryPointers : ARRAY [0..99] OF POINTER TO FileEntry;
	pointerIndex : INT;
	recursiveFileFinder : POINTER TO FileFinder;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Private" Id="{901aeac1-6b7d-4919-94a1-c714f2a47ed6}" />
    <Method Name="AddNewFileEntryToCollection" Id="{06392cf5-204a-423e-a7bb-d64aba0e4ddd}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE AddNewFileEntryToCollection
VAR_INPUT
	FilePath : FilePath;
	FileEntryData : ST_FindFileEntry;
END_VAR
VAR
	fileEntry : POINTER TO FileEntry;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[fileEntry := __NEW(FileEntry(Path:=FilePath, Name:=FileEntryData.sFileName));
THIS^.referencedFileCollection.Insert(fileEntry^);

fileEntryPointers[pointerIndex] := fileEntry;
pointerIndex := pointerIndex + 1;

ADSLOGSTR(ADSLOG_MSGTYPE_STRING, 'File added to collection: %s', fileEntry^.GetFullPath());
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ClearResults" Id="{371ae0e5-bfe1-4064-b67e-d6ba127fbabb}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE ClearResults
VAR
	i : INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[ADSLOGSTR(ADSLOG_MSGTYPE_STRING, 'Clearing pointers %s', '');

FOR i := 0 TO 99 DO
	IF (fileEntryPointers[i] <> 0) THEN
		__DELETE(fileEntryPointers[i]);
	END_IF
END_FOR

pointerIndex := 0;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_exit" Id="{ec59ab90-fd4a-49c2-bc66-4f18d345d494}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF recursiveFileFinder <> 0 THEN
	__DELETE(recursiveFileFinder);
END_IF

ClearResults();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Find" Id="{d08b7188-a356-44e3-9d70-4e2a94161fb2}">
      <Declaration><![CDATA[METHOD Find : BOOL;
VAR_INPUT
	SearchDirectory : FilePath;
	FileExtension : FileExtension;
	Collection : I_FileSystemEntryCollection;
	Append : BOOL;
END_VAR
VAR
	newSearchDir : FilePath;
	fileEntry : ST_FindFileEntry;
	fileSystemEntry : I_FileSystemEntry;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE status OF
	
	INIT:
		
		THIS^.referencedFileCollection := Collection;
		
		IF NOT Append THEN
			THIS^.referencedFileCollection.Clear();
			ClearResults();
		END_IF
		
		searchDir := SearchDirectory;
		fileExt := FileExtension;
		searchString := CONCAT(searchDir, '*');
		
		fbFindFileEntry(
			bExecute:=FALSE,
			sNetId:='',
			sPathName:=searchString,
			tTimeout:=DEFAULT_ADS_TIMEOUT
		);
		
		status := FIRST_READ;
		
	FIRST_READ:
		fbFindFileEntry(
			eCmd:=E_EnumCmdType.eEnumCmd_First,
			bExecute:=TRUE,	
		);
		
		status := WAITING_FOR_NOT_BUSY;
	
	NEXT_READ:
		fbFindFileEntry(
			eCmd:=E_EnumCmdType.eEnumCmd_Next,
			bExecute:=TRUE
		);
		
		status := WAITING_FOR_NOT_BUSY;
	
	WAITING_FOR_NOT_BUSY: 
		fbFindFileEntry(bExecute:=FALSE);
		IF NOT fbFindFileEntry.bBusy THEN
			status := CHECK_EOE;
		END_IF

	CHECK_EOE: 
		IF fbFindFileEntry.bEOE THEN
			status := FINISHED;
			RETURN;
		END_IF
		
		status := PROCESS_FOUND_ENTRY;
		
	PROCESS_FOUND_ENTRY:
		IF fbFindFileEntry.stFindFile.fileAttributes.bDirectory THEN
			status := FOUND_DIRECTORY;
			RETURN;
		END_IF
		
		status := FOUND_FILE;
		
	FOUND_DIRECTORY:
	
		IF (fbFindFileEntry.stFindFile.sFileName = '.') OR (fbFindFileEntry.stFindFile.sFileName = '..') THEN
			status := NEXT_READ;
			RETURN;
		END_IF
			
		IF (recursiveFileFinder = 0) THEN				
			recursiveFileFinder := __NEW(FileFinder);
			newSearchDir := CONCAT(searchDir, fbFindFileEntry.stFindFile.sFileName); // sFileName is the name of the directory just found.
			newSearchDir := CONCAT(newSearchDir, '\');
		END_IF

		IF (recursiveFileFinder^.Find(newSearchDir, fileExt, THIS^.referencedFileCollection, TRUE)) THEN // find files in sub-directory and append to referenced collection.
			__DELETE(recursiveFileFinder);
			status := NEXT_READ;
		END_IF

	FOUND_FILE:
		IF IsMatchingFileExtension(fbFindFileEntry.stFindFile.sFileName, fileExt) THEN
			AddNewFileEntryToCollection(searchDir, fbFindFileEntry.stFindFile);
		END_IF
		
		status := NEXT_READ;
	
	FINISHED:
		Find := TRUE;
		status := INIT;
		RETURN;
	
END_CASE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetFileExtension" Id="{ec9e558d-ad22-43fa-927d-a48c386c4531}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE GetFileExtension : FileExtension
VAR_INPUT
	In : FileName;
END_VAR
VAR
	length : INT;
	position : INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[length := Tc2_Standard.LEN(In);
position := Tc2_Standard.FIND(In, '.');

GetFileExtension := Tc2_Standard.RIGHT(In, (length - position) + 1);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="IsMatchingFileExtension" Id="{d091fd4f-bfcb-47e0-b9e6-df77b7374b88}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE IsMatchingFileExtension : BOOL
VAR_INPUT
	In : FileName;
	Extension : FileExtension;
END_VAR
VAR
	extension1 : FileExtension;
	extension2 : FileExtension;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[extension1 := GetFileExtension(In);
extension2 := GetFileExtension(Extension);

IsMatchingFileExtension := (extension1 = extension2);
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FileFinder">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.AddNewFileEntryToCollection">
      <LineId Id="5" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="45" Count="0" />
      <LineId Id="43" Count="1" />
      <LineId Id="46" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="25" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.ClearResults">
      <LineId Id="50" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="54" Count="2" />
      <LineId Id="44" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="58" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.FB_exit">
      <LineId Id="14" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="23" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.Find">
      <LineId Id="1032" Count="2" />
      <LineId Id="1241" Count="0" />
      <LineId Id="1293" Count="1" />
      <LineId Id="1290" Count="1" />
      <LineId Id="1295" Count="0" />
      <LineId Id="1292" Count="0" />
      <LineId Id="1243" Count="0" />
      <LineId Id="1035" Count="51" />
      <LineId Id="1184" Count="0" />
      <LineId Id="1087" Count="11" />
      <LineId Id="1282" Count="1" />
      <LineId Id="1102" Count="3" />
      <LineId Id="1108" Count="10" />
      <LineId Id="270" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.GetFileExtension">
      <LineId Id="6" Count="3" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FileFinder.IsMatchingFileExtension">
      <LineId Id="38" Count="0" />
      <LineId Id="41" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="42" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>